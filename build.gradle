plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.6
targetCompatibility = 1.6

group 'com.transloadit.sdk'
version = '0.0.2'
description = "A Java Integration of the Transloadit's(https://transloadit.com) file uploading and encoding service."

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}


dependencies {
    compile 'org.jetbrains:annotations:13.0'
    compile 'io.tus.java.client:tus-java-client:0.3.2-pre1'
    compile 'joda-time:joda-time:2.9.7'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'org.json:json:20160212'
    compile 'commons-codec:commons-codec:1.9'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mock-server:mockserver-netty:3.9.17'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

def pomConfig = {
    name 'transloadit'
    url 'https://github.com/transloadit/java-sdk'
    licenses {
        license {
            name 'The MIT License (MIT)'
            url 'http://opensource.org/licenses/MIT'
        }
    }

    developers {
        developer {
            id 'ifedapoolarewaju'
            name 'Ifedapo Olarewaju'
            email 'ifedapoolarewaju@gmail.com'
        }
    }

    scm {
        url 'https://github.com/transloadit/java-sdk'
        connection 'https://github.com/transloadit/java-sdk'
        developerConnection 'https://github.com/transloadit/java-sdk'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId project.getGroup()
            artifactId 'transloadit'
            version project.getVersion()
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', project.getDescription())
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    dryRun = true
    pkg {
        repo = 'maven'
        name = 'transloadit'
        userOrg = 'transloadit'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/transloadit/java-sdk.git'
        websiteUrl = 'https://github.com/transloadit/java-sdk'
        issueTrackerUrl = 'https://github.com/transloadit/java-sdk/issues'
        publicDownloadNumbers = true
        version {
            name = project.getVersion()
            desc = project.getDescription()
            vcsTag = project.getVersion()
        }
    }
}